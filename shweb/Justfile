set windows-shell := ["pwsh.exe", "-c"]

www := "www"
wasm := www / "shweb-wasm"
dist := "dist"

clean: clean-wasm clean-dist

[unix]
clean-wasm:
  rm -rf "{{wasm}}"

[windows]
clean-wasm:
  if (Test-Path "{{wasm}}") { Remove-Item -Recurse -Force "{{wasm}}" }

[unix]
clean-dist:
  rm -rf "{{dist}}"

[windows]
clean-dist:
  if (Test-Path "{{dist}}") { Remove-Item -Recurse -Force "{{dist}}" }

build-wasm $RUSTFLAGS='-Ctarget-feature=+relaxed-simd,+sign-ext,+simd128,+tail-call': clean-wasm
  wasm-pack build --target web --release --out-dir "{{wasm}}"

[unix]
build: clean build-wasm
  mkdir -p "{{dist}}"

  cp -r "{{www}}"/* "{{dist}}"

[windows]
build: clean build-wasm
  New-Item -ItemType Directory -Path "{{dist}}" | Out-Null

  Copy-Item -Path "{{www}}/*" -Destination "{{dist}}" -Recurse
